// https://github.com/pporkka/DefenderForCloudQueries/
// This query fetches secure scores for all subscriptions with certain tag+value. If you for example have insane number of subscriptions, with different purposes, this might be it.
// Secure score for "Prod" subscriptions (tag: workload) + some without any usefull tags. Change the tag "Workload" and tagvalue "Prod" to fulfill your needs.
// See the readme for information on what to remove if tag filtering is not needed
// run in Azure Resource Graph Explorer
// First the subscriptions so that the query returns everything, even those that dont have secure scores (or you don't see it) for some reason.
resourcecontainers
| where type == 'microsoft.resources/subscriptions'
| mvexpand tags
| extend tagKey = tostring(bag_keys(tags)[0])
| extend tagValue = tostring(tags[tagKey])
| where (tagKey =~ "Workload" and tagValue =~"Prod")  //or (name contains 'somethingsomething')
| project subscriptionName = name, subscriptionId
| distinct subscriptionName, subscriptionId
| join kind = leftouter(
securityresources
| where type == 'microsoft.security/securescores'
| extend current = case(isnotempty(properties.score.current), properties.score.current, 0), max = todouble(case(isnotempty(properties.score.max),properties.score.max,0)))
    on subscriptionId
| project subscriptionName, securescore = strcat(round(((current / max)*100),0),"%"), current, max 
| sort by subscriptionName asc
